### http://www.ebi.ac.uk/rdf/services/reactome/sparql
### Pathways that references Insulin (http://purl.uniprot.org/uniprot/P01308)

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX biopax3: <http://www.biopax.org/release/biopax-level3.owl#>

SELECT DISTINCT ?pathway ?pathwayname
WHERE
{
  ?pathway rdf:type biopax3:Pathway .
  ?pathway biopax3:displayName ?pathwayname .
  ?pathway biopax3:pathwayComponent ?reaction .
  ?reaction rdf:type biopax3:BiochemicalReaction .
  {
    { ?reaction ?rel ?protein . }
    UNION
    {
      ?reaction  ?rel  ?complex .
      ?complex rdf:type biopax3:Complex .
      ?complex ?comp ?protein .
    }
  }
  ?protein rdf:type biopax3:Protein .
  ?protein biopax3:entityReference <http://purl.uniprot.org/uniprot/P01308>
}
#LIMIT 100
